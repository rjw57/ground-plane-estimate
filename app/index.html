<!DOCTYPE html>
<html>

<head>
    <link rel="stylesheet" href="http://jsxgraph.uni-bayreuth.de/distrib/jsxgraph.css" />
    <link rel="stylesheet" href="style.css" />
</head>

<body>
<div id="floor-pane" class="split split-horizontal">
    <div id="floor-preview" class="split content"></div>
</div>
<div id="image-pane" class="split split-horizontal">
    <div id="image-ui" class="split content"></div>
</div>

<script type="x-shader/vertex" id="previewVertexShader">
varying vec2 floorCoord;

// vector giving [left, top, right, bottom] co-ords.
uniform vec4 viewBounds;
void main() {
    vec4 modelPosition = modelMatrix * vec4(position, 1.0);
    floorCoord = uv * (viewBounds.zy - viewBounds.xw);
    floorCoord += viewBounds.xw;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
</script>
<script type="x-shader/fragment" id="previewFragmentShader">
varying vec2 floorCoord;

uniform vec2 textureSize;
uniform sampler2D image;
// Barrel/pincushion distortion coefficient.
// See https://en.wikipedia.org/wiki/Distortion_(optics)
uniform float barrelPercent;
uniform mat4 worldToImageMatrix;
uniform float floorRadius;

vec2 undistortImgToDistortImg(vec2 coord) {
    // convert co-ordinate to 0,0 == centre
    // convert co-ordinate to x in range [-1, 1]
    coord -= 0.5 * textureSize;
    coord /= 0.5 * textureSize.x;

    // scale coord
    float r2 = dot(coord, coord);
    float k = barrelPercent;

    // r -> r + k * r^3 
    // d/dr = 1 + 3kr^2
    if(1.0 + 3.0 * k * r2 > 0.0) {
        coord *= 1.0 + k * r2;
    } else {
        coord *= 1e20;
    }

    // convert back
    coord *= 0.5 * textureSize.x;
    coord += 0.5 * textureSize;

    return coord;
}

void main() {
    vec4 imgCoordHomog = worldToImageMatrix * vec4(floorCoord, 0.0, 1.0);
    vec2 imgCoord = imgCoordHomog.xy / imgCoordHomog.w;

    // Correct distortion
    vec2 texCoord = undistortImgToDistortImg(imgCoord) / textureSize;

    float alpha = 1.0 - smoothstep(0.8 * floorRadius, 1.2 * floorRadius,
        length(floorCoord));

    // Clip to image
    if((imgCoordHomog.w > 0.0) && (texCoord.x >= 0.0) && (texCoord.y >= 0.0)
            && (texCoord.x < 1.0) && (texCoord.y < 1.0)) {
        gl_FragColor = alpha * texture2D(image, texCoord)
            + (1.0 - alpha) * vec4(0.0, 0.0, 0.0, 0.0);
    } else{
        gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
    }
}
</script>

<!-- HACK: place GLSL code here for extraction. -->
<script type="x-shader/vertex" id="imageVertexShader">
varying vec2 imgCoord;
uniform vec4 viewBounds;

void main() {
    imgCoord = uv * (viewBounds.zy - viewBounds.xw);
    imgCoord += viewBounds.xw;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
</script>
<script type="x-shader/fragment" id="imageFragmentShader">
varying vec2 imgCoord;

uniform vec2 textureSize;
uniform sampler2D image;

// Barrel/pincushion distortion coefficient.
// See https://en.wikipedia.org/wiki/Distortion_(optics)
uniform float barrelPercent;

vec2 undistortImgToDistortImg(vec2 coord) {
    // convert co-ordinate to 0,0 == centre
    // convert co-ordinate to x in range [-1, 1]
    coord -= 0.5 * textureSize;
    coord /= 0.5 * textureSize.x;

    // scale coord
    float r2 = dot(coord, coord);
    float k = barrelPercent;

    // r -> r + k * r^3 
    // d/dr = 1 + 3kr^2
    if(1.0 + 3.0 * k * r2 > 0.0) {
        coord *= 1.0 + k * r2;
    } else {
        coord *= 1e20;
    }

    // convert back
    coord *= 0.5 * textureSize.x;
    coord += 0.5 * textureSize;

    return coord;
}

void main() {
    vec2 distortImgCoord = undistortImgToDistortImg(imgCoord);
    vec2 texCoord = distortImgCoord / textureSize;
    vec2 clipCoord = distortImgCoord / textureSize;
    if((clipCoord.x >= 0.0) && (clipCoord.y >= 0.0)
            && (clipCoord.x < 1.0) && (clipCoord.y < 1.0)) {
        gl_FragColor = texture2D(image, texCoord);
        //gl_FragColor = vec4(floor(imgCoord / 1920.0), 0.0, 1.0);
    } else {
        gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
    }
}
</script>

<script type="x-shader/vertex" id="floorVertexShader">
varying vec2 imgCoord;
uniform vec4 viewBounds;
void main() {
    imgCoord = uv * (viewBounds.zy - viewBounds.xw);
    imgCoord += viewBounds.xw;
    vec4 modelPosition = modelMatrix * vec4(position, 1.0);
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
</script>
<script type="x-shader/fragment" id="floorFragmentShader">
// #extension GL_OES_standard_derivatives : enable

varying vec2 imgCoord;
uniform mat3 imageToFloorMatrix;
uniform float floorOpacity, floorRadius;

void main() {
    vec3 floorCoordHomog = imageToFloorMatrix * vec3(imgCoord, 1.0);
    if(floorCoordHomog.z <= 0.0) {
        gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
    } else {
        float floorBrightness = 0.5;

        vec2 floorCoord = floorCoordHomog.xy / floorCoordHomog.z;
        float floorRad = length(floorCoord);

        // want 1x1m checkerboard
        vec2 floorFrac = fract(0.5 * floorCoord) - 0.5;
        float checkerQuad = floorFrac.x * floorFrac.y;
        floorBrightness = (checkerQuad > 0.0) ? 0.75 : 0.25;

        vec3 floorColour = vec3(1.0, 1.0, 1.0);
        if(dot(floorCoord, floorCoord) < 0.5 * 0.5) {
            floorColour = vec3(1.0, 0.0, 0.0);
        }

        float alpha = floorOpacity *
            (1.0 - smoothstep(0.8 * floorRadius, 1.2 * floorRadius, floorRad));
        gl_FragColor = vec4(floorColour * floorBrightness, alpha);
    }
}
</script>

    <script src="http://jsxgraph.uni-bayreuth.de/distrib/jsxgraphcore.js"></script>
    <script src="http://www.numericjs.com/lib/numeric-1.2.6.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r73/three.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/dat-gui/0.5.1/dat.gui.min.js"></script>
    <script src="FileSaver.min.js"></script>
    <script src="split.min.js"></script>
    <script src="app.js"></script>
</body>

</html>
